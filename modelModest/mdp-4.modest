// From Lecture Note Exercise 8.1
action r, s;

bool successful= false;
bool failure = false;
transient int(0..1) countTransitions = 0;

property P0 = Pmax(<> [S(countTransitions) <= 4](successful == true));
property P1 = Pmax(<> (successful == true));
property P2 = Pmax(<> (failure == true));
property P3 = Pmin(<> (successful == true));
property P4 = Pmin(<> (failure == true));

transient int(0..8) reward = 0;
property R1 = Xmax(S(reward), successful == true || failure == true);
property R2 = Xmin(S(reward), successful == true);

process Choose()
{
	alt {
	:: r {= countTransitions = 1 =}; Risk()
	:: s {= countTransitions = 1 =}; Safe()
	}
}

process Safe()
{
	tau palt {
	:0.1: {= countTransitions = 1, successful = true, reward = 2 =}; Success()
	:0.9: {= countTransitions = 1 =}; Choose()
	}
}

process Risk()
{
	r palt {
    :0.5: {= countTransitions = 1, successful = true, reward = 8 =}; Success()
	:0.5: {= countTransitions = 1, failure = true =}; Fail()
    }
}

process Fail() { tau; Fail() }
process Success() { tau; Success() }

Choose()
